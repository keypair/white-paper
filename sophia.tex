\section{Sophia smart contracts}
\label{sect:sophia}

Smart contracts \cite{szabo1996smart,hhz007} are
programs on the blockchain that can perform tasks with the data on that
blockchain. Typically contracts have state (data), which is recorded on the
chain. A call to a function in the contract results in a return value
and updated state, both put back on the chain as a result of the
call.

Smart contracts are an active research field
\cite{DBLP:journals/corr/abs-1710-06372} and a substantial amount of
  effort goes in to studying the verification and validation of smart contracts
  \cite{magazzeni2017validation,bhargavan2016formal}.

There are two major technical challenges for smart contract
implementations. The first challenge is to make the contracts
execute fast without requiring too many resources. In a blockchain
implementation, contract execution is performed in a
\textit{virtual machine}. This is an execution engine with formally
defined semantics, such that all implementations perform exactly the
same computation steps, with exactly the same result and charging
exactly the same amount of gas. The \blockchain\ defines two virtual
machines, the AEVM, compatible with the Ethereum blockchain, and the more
efficient FATE virtual machine.

The second challenge is to design a language to express contracts in
such a way that one can understand and reason about the contract
both as a human, but also mechanically by computer programs. The
language should by design protect contract designers against
vulnerabilities that can be exploited. Sophia is a functional language
to accommodate for these properties. It is designed as a contract
language with security and user comfort in mind. In particular,
vulnerabilities in contracts in other languages
\cite{atzei2017survey,mehar2019understanding,delmolino2016step} have been
studied with the goal to
avoid the possibility to make such mistakes in Sophia.

% Contracts are put on chain by contract create
% transactions, specifying their byte code and data to compute the
% initial state. A contract on chain is called by contract call
% transactions\footnote{in
%  \aet\ contracts are executed by explicit calls, there is no
%  mechanism to automatically progress computation.}. Each contract
% creation and contract call requires computation effort and the user is paying
% for that effort by paying for \textit{gas}. Each operation is
% associated with a certain amount of gas and the total used gas is paid
% for. The user specifies how much gas is provided in the transaction. A
% surplus is returned. If there is too little gas provided, the
% transaction is accepted (it is recorded
% on the chain), and the user pays for transaction fee and gas,
% but the contract computation fails and the contract state remains unchanged.

\subsection{The Sophia language}

Sophia is a functional programming language \cite{hughes1989functional}.
The main unit of code in Sophia is the \textit{contract}.
A contract implementation, or simply a contract, is the code for a
smart contract and consists of a list of types, entrypoints and local
functions. Only the entrypoints can be called from outside the
contract.   A \textit{contract instance} is an entity living on the
blockchain (or in a state channel). Each instance has an address that
can be used to call its entrypoints, either from another contract or
in a call transaction.  A contract may define a type \texttt{state}
encapsulating its local state. When creating a new contract the
\texttt{init} entrypoint is executed and the state is initialized to its
return value.


\subsubsection{Dutch auction contract}

As an example, let us consider an auction contract.  In such an
auction contract, a user could auction an object in the real world by
creating and posting a contract to the blockchain, using a \textbf{contract
create transaction}. Let us assume that
this is a Dutch auction, then the initial price
would be set high and for each new key-block that is mined (representing
time) the price is decreased. Someone buys the object by calling a
\texttt{bid} function. When this bidding \textbf{contract call transaction}
executes,
the contract computes the price given the current block number; if the caller
has
supplied enough funds in that call, the seller is paid, the
bidder is charged (possibly refunded the extras) and the contract
enters a non sellable state for the object. The next bidder will fail
the call and only pays for transaction costs, not for the object.

The complete Sophia code for a Dutch auction is presented here:

\begin{verbatim}
contract DutchAuction =

  record state = { amount : int,
                   height : int,
                   dec    : int,
                   sold   : bool }

  entrypoint init(price, decrease) : state =
    { amount = price,
      height = Chain.block_height,
      dec    = decrease,
      sold   = false }

  stateful payable entrypoint bid() =
    require( !state.sold, "sold"  )
    let price = demanded_price()
    require( Contract.balance >= price, "not enough tokens" )
    Chain.spend(Contract.creator, price)
    Chain.spend(Call.origin, Contract.balance)
    put(state{sold = true})

  function demanded_price() : int =
    state.amount - (Chain.block_height - state.height) * state.dec

\end{verbatim}

The contract languages and hence the evaluation in the virtual
machine, must have access to blockchain primitives like the height of
the chain and caller accounts. Typically, all blockchain
primitives are available from within a contract.

Note that the contract create transaction includes the contract
byte code, not the source code, together with information on
which version of the compiler is used. Compiled for FATE this contract
results in 254 bytes, whereas compiled for the AEVM 2092 bytes are
needed. The gas needed to compute the initial state is 240 for FATE compared
to 741 for the AEVM.

The \texttt{init} function is called when the contract is created to
compute the initial state of the contract. The \texttt{init} function
is not part of the byte code, such that it cannot accidentally be
called again. If one wants to reset the state, this has to be
explicitly programmed to avoid expensive exploits
\cite{suiche2017280}.
The contract designer also has to explicitly mention whether the
state of the contract is changed in a call (using the
\texttt{stateful} keyword).
Entrypoints can be called from outside the contract, whereas functions
are only accessible from within the contract.

The keyword \texttt{payable} is added to explicitly state which
function calls expect to come with additional tokens in the contract
call transaction. These tokens are added to the contract balance
before the call is made. If, however, the call is reverted by a failing
\texttt{require}d condition, then the provided tokens are returned.

The transparency of the blockchain guarantees that it is verifiable
that the first valid bid on chain\footnote{The \blockchain\
  does not guarantee that the first one posting a valid bid becomes
  the first one on chain.} is correctly paying the right
price. Moreover, it is verifiable that the bidding call transactions accepted
later are
only charged a transaction fee and the cost of execution.
The sale conditions are transparent, but whether the actual object ever
arrives is outside the scope of the blockchain


\subsubsection{Oracles example}

An example of the use of oracles and contracts can be illustrated with
a little lottery example. Note that all computation on a blockchain
should be deterministic in order to be able to validate the
results. If not exactly the same, the corresponding state hashes will
differ. As a consequence there is no random number generator in the
Sophia language\footnote{Even if some kind of random function would be
  offered, it would be deterministic and hence have a predictable
  outcome}. (Instead, the current blockhash could be used as entropy.)

Running a simple lottery in which users buy a ticket and after a while
one draws one of the tickets as the winner, is somehow depending on
some kind of fair randomness. If the number is known or computable at
the start of the buying process, one might be able to figure out what
ticket to buy to win the lottery. But if we close the lottery and then
ask an oracle for a random number, then a trusted, but disconnected
computation can be used to draw the winner.

So assume there is such an oracle, monitoring the chain and registered
with a reasonable query fee covering for its cost of operation. The
oracle has an identifier, for the example say
\begin{quote}
  \textit{"ok\_shEHMV8Q2F1HR86pcyF7DYpudg8hnvJwJuVE3berWpbktnL2R"}.
\end{quote}
We can now write a Sophia contract that takes this oracle identifier
as input of its initialization and uses it for random numbers in the
lottery game.

\begin{verbatim}
include "List.aes"

contract Lottery =

  record state = { participants : list(address),
                   price_sum : int(),
                   close_height : int(),
                   oracle : oracle(int, int),
                   query : option(oracle_query(int, int))  }

  entrypoint init(rand : oracle(int, int)) : state =
    { participants = [],
      price_sum = 0,
      close_height = 0,
      oracle = rand,
      query = None }
\end{verbatim}

The contract stores a list of participants in its state, a price sum
that increases for each ticket bought, a closing height after which no
new tickets can be purchased. The initial closing height is zero,
because initially there is no ongoing lottery. The state also contains
an oracle and an optional query. This query will be instantiated when
the lottery is closed and a ticket is drawn.

The creator of this contract may now start the lottery by supplying a relative
closing height,
for example, 20 key-blocks from that the transaction gets on chain;
approximately one hour. Any user can then buy a ticket.
\begin{verbatim}
  stateful payable entrypoint start(n : int) =
    require( Call.caller == Contract.creator, "not creator" )
    require( state.price_sum == 0, "lottery ongoing" )
    require( n > 1, "block in future")
    put(state{ participants = [],
               close_height = Chain.block_height + n })

  stateful payable entrypoint buy() =
    require( state.close_height > Chain.block_height, "lottery closed" )
    require( Call.value == 10, "price ticket 10" )
    put(state{ participants = Call.caller :: state.participants,
               price_sum = state.price_sum + 8 })  // we take 20%
\end{verbatim}
Note that a lot of conditions are checked, resulting in abortion of
the contract when falsified. In general, making the contract safe
requires thinking through a large number of possible scenarios in
which things may not work out as expected.
For example, if no users buy a ticket\footnote{The price of the ticket
is set to $10$ aettos instead of a more realistic
$10\_000\_000\_000\_000\_000$ to
make the code more readable}, the contract creator must be
able to restart the lottery at a later time, but the creator should
not be able to restart as soon as there are participants. Similarly,
one should not allow participants to buy tickets when the lottery is
closed.

The keyword \verb+payable+ expresses that we expect the participants
to add a token amount to the contract call transaction, which is
checked by comparing \verb+Call.value+. Similarly, the contract
creator needs to pay something into the contract to cover the oracle query
fee\footnote{Note that there is no check in the contract that it has
  enough funds to query the oracle after starting a lottery. The
  participants are at risk here} in case there are too few
participants.

When the closing height is reached, someone, most likely the creator
of the contract, asks the oracle to draw a number between 1 and the
number of participants. This call returns the query., such that anyone
can monitor the chain to see if the query has arrived. When that's the
case, the winner, or anyone else, can call the claim function, which
will transfer the price sum to the winner.
\begin{verbatim}
 stateful entrypoint draw() : oracle_query(int, int) =
     require( Chain.block_height > state.close_height, "ongoing lottery" )
     require( state.price_sum > 0, "no ongoing lottery" )
     require( state.query == None, "already drawn" )
     let q =
        Oracle.query(state.oracle, List.length(state.participants) - 1,
                     Oracle.query_fee(state.oracle),
                     RelativeTTL(5), RelativeTTL(480))
     put(state{query = Some(q)})
     q

  stateful entrypoint claim() : option(address) =
    switch(state.query)
        None => abort( "no drawing" )
        Some(query) =>
          switch( Oracle.get_answer(state.oracle, query))
             None => abort("waiting for query")
             Some(winner) =>
               let winner_account = List.get(winner, state.participants)
               // Spend to winner
               Chain.spend(winner_account, state.price_sum)
               put(state{ price_sum = 0, query = None })
               Some(winner_account)
\end{verbatim}

The reason to make it possible for anyone to call these functions is
to ensure that the contract creator can force progress to start a new
lottery and the winner to be able to claim even if the contract owner
is not around. The TTLs in the query assure that the oracle has
approximately 15 minutes to answer, long enough to even get it out
in busy times. Within a day one then has to claim the price sum,
otherwise, because the query answer is then no longer on chain.

This contract is far from fully secure, but illustrates an example of
how oracles and contracts can be used together.

%TODO \subsubsection{Oracles and state channel example}


\subsection{Readable Smart Contracts in Lexon}
Human readable smart contracts compiling to Sophia.
...

\subsection{The FATE virtual machine}
\label{sect:fate}

The Fast Aeternity Transaction Engine (FATE) VM uses transactions as
its basic operations and operates directly on the state tree of the
\aet\ chain. This enables native integration with first class
objects such as oracles, the naming system, and state channels since
those are all managed by specific types of transactions described on
the protocol level. FATE is a simple-to-use machine language, superior
to the more traditional byte-code virtual machines currently used on
other platforms. It enables easier, safe coding, faster transactions,
and smaller code sizes. It is custom-built to seamlessly integrate
with the functional smart contract language Sophia.

\subsubsection{More secure}

Every operation and every value is typed. Any type violation results in
an exception and reverts all state changes. This prevents people to
circumvent the compiler and write or modify their own FATE code to use
type violations as an attack vector.

The instruction memory is divided into functions and basic
blocks. Only basic blocks can be indexed and used as jump
destinations. This is a precaution to be unable to jump to arbitrary
positions in memory. It also fit FATE's function style by
having function calls instead of jumps. Moreover, data and control
flow are separated, one cannot possibly modify the running
contract, since the code memory cannot be written to.

FATE is “functional” in the sense that “updates” of data structures,
such as tuples, lists or maps do not change the old values of the
structure, instead a new version is created.  FATE does have the
ability to write the value of an operation back to the same register
or stack position as one of the arguments, in effect updating the
memory.

FATE solves a fundamental problem programmers run into when coding for
Ethereum: integer overflow, weak type checking and poor data
flow. FATE checks all arithmetic operations to keep the right meaning
of it. Integers cannot overflow, since FATE uses unbounded integer
arithmetic (cf.\ Bignums \cite{serpette1989bignum}). Floats are not
part of the language, avoiding a bunch of issues associated with
floating point arithmetic. Also you cannot cast types (e.g\ integers to
booleans). This makes
FATE ultimately a safer coding platform for smart contracts.

\subsubsection{More efficient}

FATE uses high level instructions. There are instructions to operate
on the chain state tree in a safe and formalized way. Likewise the
virtual machine has high-level support for most of the transactions
available on the \blockchain. There are operations such as
`ORACLE\_CHECK\_QUERY' for querying an oracle or `AENS\_CLAIM' for
claiming a name.

Having higher level instructions makes the code
deployed smaller and it reduces the blockchain size. FATE contracts
use on average ten times less space than the same contract compiled to
the AEVM, the Ethereum compatible VM. At the same time, it performs on
average much faster and uses therefore less gas.

FATE byte code by itself is already a readable program. For example, the bid
function of the Dutch auction contract compiles to this code:

\begin{verbatim}
FUNCTION bid( ) : {tuple,[]}
  ;; BB : 0
          ELEMENT a 3 store1
          NOT a a
          JUMPIF a 2
  ;; BB : 1
          ABORT "sold"
  ;; BB : 2
          CALL "(h:p"
  ;; BB : 3
          POP var1
          BALANCE a
          EGT a a var1
          JUMPIF a 5
  ;; BB : 4
          ABORT "not enough tokens"
  ;; BB : 5
          CREATOR a
          SPEND a var1
          BALANCE a
          ORIGIN a
          SPEND a a
          SETELEMENT store1 3 store1 true
          RETURNR ()
\end{verbatim}

The notion \verb+BB+ stands for basic block and jumps are always to
such a basic block. Note that for example `CREATOR', `SPEND' and
`BALANCE' are native instructions used in basic block 5. The
instruction \verb+ CALL "(h:p"+ in basic block 2 looks a bit cryptic for a call
to  the
function \verb+demanded_price()+. Each function name is hashed to 4
bytes that are printed as a string.

Both memory constraints and computation efficiency are important to
enable smaller contracts to to get more computation into a micro-block.

